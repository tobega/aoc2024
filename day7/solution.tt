composer parse-test-equation
  { test-value: <INT"1"> (<=':'>) equation: [<number>+] }
  rule number: (<WS>) <INT"1">
end parse-test-equation

def equations: [$IN::lines -> parse-test-equation];

composer to-scalar
  <INT"1">
end to-scalar

operator (left do-op&{op:} right)
  $op -> #
  when <=op´'+'> do $left + $right !
  when <=op´'*'> do $left * $right !
  when <=op´'||'> do '$left::raw;$right::raw;' -> to-scalar !
end do-op

templates try-equation&{operators:}
  @: 0"1";
  { test-value: $.test-value, equation: $.equation(first~..last), partial-result: $.equation(first) } -> #
  $@!
  when <{equation: <[](0)>, partial-result: <=$.test-value>}> do @:$.test-value;
  when <{equation: <[](0)>}> do !VOID
  otherwise
    def current: $;
    $operators... -> {
        partial-result: ($current.partial-result do-op&{op: $} $current.equation(first)),
        equation: $current.equation(first~..last),
        test-value: $current.test-value
    } -> #
end try-equation

$equations... -> try-equation&{operators: [op´'+', op´'*']} -> ..=Sum&{of: :()} -> !OUT::write
'
' -> !OUT::write
$equations... -> try-equation&{operators: [op´'+', op´'*', op´'||']} -> ..=Sum&{of: :()} -> !OUT::write
